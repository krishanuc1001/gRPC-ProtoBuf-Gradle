plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version "${googleProtobufPluginVersion}"
}

group 'org.grpc.protobuf'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${googleProtobufProtocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcProtobufVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets.main.java.srcDir new File(buildDir, 'generated/source')

dependencies {
    // grpc / proto related dependencies
    implementation "io.grpc:grpc-protobuf:${grpcProtobufVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcProtobufVersion}"
    implementation "io.grpc:grpc-stub:${grpcProtobufVersion}"
    implementation "com.google.protobuf:protobuf-java:${googleProtobufJavaVersion}"

    // necessary for Java version 9+
    implementation "org.apache.tomcat:annotations-api:${annotationsApiVersion}"

    // logback
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    // JSON serialization and de-serialization
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
}

tasks.register('customProtoc', Exec) {
    workingDir projectDir
    commandLine "protoc", "-I=src/main/proto", "--java_out=src/main/java", "--grpc_out=src/main/java", "src/main/proto/*.proto"
}

//compileJava.dependsOn customProtoc

compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
}

test {
    useJUnitPlatform()
}